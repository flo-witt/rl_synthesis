pomdp

const int N = 5;

observable "start" = pos=0;
observable "aifpos1" = pos=1 ? a : -1;
observable "decide" = pos=N;
observable "ended" = pos>N;

module remember
    pos : [0..N+2] init 0;
    a : [0..1] init 0;
    [a] pos=0 -> 0.5 : (pos'=1) & (a'=0) + 0.5 : (pos'=1) & (a'=1);
    [a] pos>0 & pos<N -> 1 : (pos'=pos + 1);
    [b] pos>0 & pos<N -> 1 : (pos'=pos + 1);

    [a] pos=N & a=0 -> 1 : (pos'=N+1);
    [b] pos=N & a=0 -> 1 : (pos'=N+2);

    [b] pos=N & a=1 -> 1 : (pos'=N+1);
    [a] pos=N & a=1 -> 1 : (pos'=N+2);

    [a] pos>N -> 1 : (pos'=pos);
endmodule

label "goal" = pos=N+1;
label "notbad" = pos!=N+2;

rewards "steps"
    true : 1;
endrewards
